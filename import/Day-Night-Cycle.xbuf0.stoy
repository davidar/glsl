/*!
 * <info>
 * <author>davidar [ https://www.shadertoy.com/user/davidar ]</author>
 * <name>Day-Night Cycle</name>
 * 
 * <description>
 *   [url]https://svs.gsfc.nasa.gov/30082[/url]
 * </description>
 * 
 * <url>https://www.shadertoy.com/view/MldyDH</url>
 * 
 * <date>2018-07-25</date>
 * 
 * <tags></tags>
 * 
 * <pass-name>Buf A</pass-name>
 * 
 * <synthclipse-importer-legal-note>
 *   As noted in: [ https://www.shadertoy.com/terms ]:
 *   If the author did not stated otherwise, this shader is licensed under
 *   Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License 
 *   [ http://creativecommons.org/licenses/by-nc-sa/3.0/deed.en_US ].
 * </synthclipse-importer-legal-note>
 * </info>
 */

//! <group name="Buf A" />

uniform vec3 iResolution;           // viewport resolution (in pixels)
uniform float iTime;                // shader playback time (in seconds)
uniform float iGlobalTime;          // shader playback time (in seconds) - <deprecated>
uniform float iTimeDelta;           // render time (in seconds)
uniform int iFrame;                 // shader playback frame
uniform float iChannelTime[4];      // channel playback time (in seconds)
uniform vec3 iChannelResolution[4]; // channel resolution (in pixels)
uniform vec4 iMouse;                // mouse pixel coords. xy: current (if MLB down), zw: click
uniform vec4 iDate;                 // (year, month, day, time in seconds)
uniform float iSampleRate;          // sound sample rate (i.e., 44100)
uniform float iFrameRate;           // frames per second (effectively "1.0 / iTimeDelta")

uniform sampler2D iChannel0; //! buffer[xbuf: 0, mipmap: true]

#include "Day-Night-Cycle.frag"

float line(vec2 a, vec2 b, vec2 p, float width) {
    // http://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm
    vec2 pa = p - a, ba = b - a;
	float h = clamp(dot(pa,ba) / dot(ba,ba), 0., 1.);	
    float d = length(pa - ba * h);
    float x = distance(p,a) / (distance(p,a) + distance(p,b));
    return 1.5 * mix(rexp(a), rexp(b), x) * smoothstep(width/2., 0., d) * smoothstep(1.75, 0.5, distance(a,b));
}

float network(vec2 p, float width) {
    // based on https://www.shadertoy.com/view/lscczl
    vec2 c = floor(p) + hash22(floor(p));
    vec2 n = floor(p) + N + hash22(floor(p) + N);
    vec2 e = floor(p) + E + hash22(floor(p) + E);
    vec2 s = floor(p) + S + hash22(floor(p) + S);
    vec2 w = floor(p) + W + hash22(floor(p) + W);
    
    float m = 0.;
    m += line(n, e, p, width);
	m += line(e, s, p, width);
    m += line(s, w, p, width);
    m += line(w, n, p, width);
   
    for (float y = -1.; y <= 1.; y++) {
        for (float x = -1.; x <= 1.; x++) {
            vec2 q = floor(p) + vec2(x,y) + hash22(floor(p) + vec2(x,y));
            float intensity = distance(p,q) / clamp(rexp(floor(p) + vec2(x,y)), 0., 1.);
            m += line(c, q, p, width);
            m += 10. * exp(-40. * intensity);
        }
    }
    
    return m;
}

void mainImage( inout vec4 fragColor, in vec2 fragCoord ) {
    if (iFrame > 10) {
        fragColor = textureLod(iChannel0, fragCoord/iResolution.xy, 0.);
        if (fragColor.zw == iResolution.xy) return;
    }
    
    float lat = 180. * fragCoord.y/iResolution.y - 90.;
    float lon = 360. * fragCoord.x/iResolution.x;
    vec3 p = fromlatlon(lat, lon);
    
    vec2 uv = fragCoord / iResolution.y + 1.;
    vec2 wiggle = vec2(FBM(vec3(50. * uv, 1)), FBM(vec3(50. * uv, 2))) - 0.5;
    
    float height = FBM(3. * p) - 0.5;
    fragColor.x = height;
    if (height < 0.) {
        fragColor.y = 0.;
    } else {
        float d = 0.75;
        float width = 3e-3;
        d += 0.5 * network(100. * uv + 1.0 * wiggle, 100. * width);
        d += 1.0 * network( 30. * uv + 0.3 * wiggle,  30. * width);
        d += 2.0 * network( 10. * uv + 0.1 * wiggle,  10. * width);
        d += smoothstep(0.1, 0., height); // coast
        d *= 0.1 + clamp(2. * FBM(12. * p) - 0.5, 0., 1.);
        d *= 0.2 + 1.3 * clamp(2. * FBM(1.5 * p) - 0.67, 0., 1.);
        fragColor.y = d;
    }
    
    fragColor.zw = iResolution.xy;
}


void main() {
	vec4 color = vec4(0.0, 0.0, 0.0, 1.0);
	mainImage(color, gl_FragCoord.xy);

	gl_FragColor = color;
}