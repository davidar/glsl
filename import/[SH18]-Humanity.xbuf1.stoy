/*!
 * <info>
 * <author>davidar [ https://www.shadertoy.com/user/davidar ]</author>
 * <name>[SH18] Humanity</name>
 * 
 * <description>
 *   A short story about humanity, from a global perspective. Followed by a minigame
 *   in the spirit of SimEarth (see below to enable). Restart to sync music. Read
 *   the comment at the top of the code for commentary and instructions.
 * </description>
 * 
 * <url>https://www.shadertoy.com/view/XttcWn</url>
 * 
 * <date>2018-08-04</date>
 * 
 * <tags></tags>
 * 
 * <pass-name>Buffer B</pass-name>
 * 
 * <synthclipse-importer-legal-note>
 *   As noted in: [ https://www.shadertoy.com/terms ]:
 *   If the author did not stated otherwise, this shader is licensed under
 *   Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License 
 *   [ http://creativecommons.org/licenses/by-nc-sa/3.0/deed.en_US ].
 * </synthclipse-importer-legal-note>
 * </info>
 */

//! <group name="Buffer B" />

uniform vec3 iResolution;           // viewport resolution (in pixels)
uniform float iTime;                // shader playback time (in seconds)
uniform float iGlobalTime;          // shader playback time (in seconds) - <deprecated>
uniform float iTimeDelta;           // render time (in seconds)
uniform int iFrame;                 // shader playback frame
uniform float iChannelTime[4];      // channel playback time (in seconds)
uniform vec3 iChannelResolution[4]; // channel resolution (in pixels)
uniform vec4 iMouse;                // mouse pixel coords. xy: current (if MLB down), zw: click
uniform vec4 iDate;                 // (year, month, day, time in seconds)
uniform float iSampleRate;          // sound sample rate (i.e., 44100)
uniform float iFrameRate;           // frames per second (effectively "1.0 / iTimeDelta")

uniform sampler2D iChannel3; //! keyboard[]
uniform sampler2D iChannel0; //! buffer[xbuf: 0, wrap: GL_CLAMP_TO_EDGE, mipmap: true]
uniform sampler2D iChannel1; //! buffer[xbuf: 1, wrap: GL_CLAMP_TO_EDGE, mipmap: false, filter: GL_LINEAR]

#include "[SH18]-Humanity.frag"

// 2018 David A Roberts <https://davidar.io>

// atmospheric pressure and circulation model

#define buf(uv) texture(iChannel1, uv)

#define SIGMA vec4(6,4,1,0)
vec4 normpdf(float x) {
	return 0.39894 * exp(-0.5 * x*x / (SIGMA*SIGMA)) / SIGMA;
}

// mean sea level pressure
vec4 mslp(vec2 uv) {
    float lat = 180. * (uv.y * iResolution.y / MAP_RES.y) - 90.;
    float y = textureLod(iChannel0, uv * MAP_ZOOM, MAP_LOD).z;
    float height = MAP_HEIGHT(y);
    vec4 r;
    if (y > OCEAN_DEPTH) { // land
        r.x = 1012.5 - 6. * cos(lat*PI/45.); // annual mean
        r.y = 15. * sin(lat*PI/90.); // January/July delta
    } else { // ocean
        r.x = 1014.5 - 20. * cos(lat*PI/30.);
        r.y = 20. * sin(lat*PI/35.) * abs(lat)/90.;
    }
    r.z = height;
    return r;
}

// horizontally blurred MSLP
vec4 pass1(vec2 uv) {
    vec4 r = vec4(0);
    for (float i = -20.; i <= 20.; i++)
        r += mslp(uv + i*E/iResolution.xy) * normpdf(i);
    return r;
}

// fully blurred MSLP
vec4 pass2(vec2 uv) {
    vec4 r = vec4(0);
    for (float i = -20.; i <= 20.; i++)
        r += buf(uv + i*N/iResolution.xy + PASS1) * normpdf(i);
    return r;
}

// time-dependent MSLP and temperature
vec4 pass3(vec2 uv) {
    vec4 c = buf(uv + PASS2);
    float t = mod(iTime/2., 12.); // simulated month of the year
    float delta = 1. - 2. * smoothstep(1.5, 4.5, t) + 2. * smoothstep(7.5, 10.5, t);
    float mbar = c.x + c.y * delta;
    
    float lat = 180. * (uv.y * iResolution.y / MAP_RES.y) - 90.;
    float land = step(OCEAN_DEPTH, textureLod(iChannel0, uv * MAP_ZOOM, MAP_LOD).z);
    float height = c.z;
    float temp = -27. + 73. * tanh(2.2 * exp(-0.5 * pow((lat + 5. * delta)/30., 2.)));
    temp -= mbar - 1012.;
    temp /= 1.8;
    temp += 1.5 * land;
    float th = 4.;
    
    if (iTime > WARMING_START_TIME && iTime < STORY_END_TIME + 15.) {
        // approximation of CO2 forcing
        // https://www.nature.com/articles/nclimate3036
        float temp2 = 9. + 50. * tanh(2.2 * exp(-0.5 * pow((lat + 5. * delta)/30., 2.)));
        temp2 -= mbar - 1012.;
        temp2 /= 1.8;
        temp2 += 4. * land;
        float th2 = 3.;
        float m = smoothstep(WARMING_START_TIME, WARMING_END_TIME, iTime);
        m -= smoothstep(5., 15., iTime - STORY_END_TIME);
        temp = mix(temp, temp2, m);
        th = mix(th, th2, m);
    }
    
    return vec4(mbar, temp - th * height, temp, 0);
}

// wind vector field
vec4 pass4(vec2 uv) {
    vec2 p = uv * iResolution.xy;
    float n = buf(mod(p + N, MAP_RES)/iResolution.xy + PASS3).x;
    float e = buf(mod(p + E, MAP_RES)/iResolution.xy + PASS3).x;
    float s = buf(mod(p + S, MAP_RES)/iResolution.xy + PASS3).x;
    float w = buf(mod(p + W, MAP_RES)/iResolution.xy + PASS3).x;
    vec2 grad = vec2(e - w, n - s) / 2.;
    float lat = 180. * fract(uv.y * iResolution.y / MAP_RES.y) - 90.;
    vec2 coriolis = 15. * sin(lat*PI/180.) * vec2(-grad.y, grad.x);
    vec2 v = coriolis - grad;
    return vec4(v,0,0);
}

void mainImage( inout vec4 fragColor, in vec2 fragCoord ) {
    vec2 uv = fragCoord / iResolution.xy;
    if (uv.x < 0.5) {
        if (uv.y < 0.5) {
    		fragColor = pass1(uv - PASS1);
        } else {
    		fragColor = pass2(uv - PASS2);
        }
    } else {
        if (uv.y < 0.5) {
    		fragColor = pass3(uv - PASS3);
        } else {
    		fragColor = pass4(uv - PASS4);
        }
    }
    
    int x = int(fragCoord.x);
    int y = int(fragCoord.y);
    if (x < 256 && y < 3) fragColor.w = texelFetch(iChannel3, ivec2(x,y), 0).x;
}


void main() {
	vec4 color = vec4(0.0, 0.0, 0.0, 1.0);
	mainImage(color, gl_FragCoord.xy);

	gl_FragColor = color;
}