#include "shadertoy.frag"
uniform sampler2D iChannel0; //! buffer[xbuf: tectonics-erosion.frag]

#define OCEAN_DEPTH 7.5

#define buf(p) texture(iChannel0,(p)/iResolution.xy)

#define N vec2( 0, 1)
#define E vec2( 1, 0)
#define S vec2( 0,-1)
#define W vec2(-1, 0)

#define PI 3.14159265359

#define STEP1 1.
#define STEP2 1.5
#define STEP3 3.
#define STEP4 10.

void mainImage(out vec4 r, in vec2 p) {
    float y = buf(p).z;
    if (iMouse.z > 0. && iMouse.x/iResolution.x > 0.5) {
        float q = buf(p).x;
        float uplift = buf(p).y;
        r.rgb = (q < 0.) ? vec3(1) : .6 + .6 * cos(2.*PI*q + vec3(0,23,21));
        r.rgb *= 0.1 + 0.9 * uplift;
    } else if (y < OCEAN_DEPTH) {
        r = mix(vec4(0.01, 0.02, 0.08, 1), vec4(0.11, 0.28, 0.51, 1), y / OCEAN_DEPTH);
    } else {
        y -= OCEAN_DEPTH;
        r = vec4(0.08, 0.14, 0.03, 1);
        r = mix(r, vec4(0.18, 0.26, 0.08, 1), smoothstep(0., STEP1, y));
        r = mix(r, vec4(0.52, 0.39, 0.26, 1), smoothstep(STEP1, STEP2, y));
        r = mix(r, vec4(0.32, 0.3, 0.2, 1), smoothstep(STEP2, STEP3, y));
        r = mix(r, vec4(1,1,1,1), smoothstep(STEP3, STEP4, y));
        
        vec2 grad = vec2(buf(p+E).z - buf(p+W).z, buf(p+N).z - buf(p+S).z);
        r.rgb *= 0.75 + 0.25 * cos(atan(grad.y, grad.x) + 0.25*PI)
                             * clamp(0.2 * length(grad), 0., 1.);
    }
}
